plugins {
	id "com.jfrog.bintray" version "1.4"
}
apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'org.squiddev'
version = '0.2.1'

targetCompatibility = sourceCompatibility = 1.6
compileTestJava.sourceCompatibility = compileTestJava.targetCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	testCompile 'org.luaj:luaj-jse:2.+'
	testCompile 'junit:junit:4.+'
	testCompile 'org.hamcrest:hamcrest-library:1.3'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['mavenJava']
	pkg {
		repo = 'maven'
		name = 'Cobalt'
		licenses = ['MIT']
		vcsUrl = 'https://github.com/SquidDev/Cobalt'

		version {
			name = project.version
			desc = 'A reentrant fork of LuaJ for Lua 5.1'
			released = new Date()
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar { classifier "sources" }
			artifact javadocJar { classifier "javadoc" }
		}
	}
}

gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
                options.compilerArgs << "-Xlint"
        }
}

task executeProfile(type: JavaExec) {
        main = "org.squiddev.cobalt.PerformanceRunner"
        classpath = sourceSets.test.runtimeClasspath
}

